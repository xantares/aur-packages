# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=gsl
pkgname=mingw-w64-$_pkgname
pkgver=1.16
pkgrel=1
pkgdesc="The GNU Scientific Library (GSL) is a modern numerical library for C and C++ programmers (mingw-w64)"
arch=(any)
url="http://www.gnu.org/software/gsl/gsl.html"
license=("GPL")
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
depends=('mingw-w64-crt')
options=('staticlibs' '!strip' '!buildflags')
source=("http://mirror.ibcp.fr/pub/gnu/$_pkgname/$_pkgname-$pkgver.tar.gz")
md5sums=('e49a664db13d81c968415cd53f62bc8b')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_pkgname}-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
