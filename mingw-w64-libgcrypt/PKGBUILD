# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=libgcrypt
pkgname=mingw-w64-$_pkgname
pkgver=1.5.3
pkgrel=4
pkgdesc="General purpose cryptographic library based on the code from GnuPG (mingw-w64)"
arch=(any)
url="http://www.gnupg.org"
license=('LGPL')
depends=('mingw-w64-crt' 'mingw-w64-libgpg-error')
makedepends=('mingw-w64-gcc' 'mingw-w64-libgpg-error')
options=('staticlibs' '!buildflags' '!strip')
source=("ftp://ftp.gnupg.org/gcrypt/${_pkgname}/${_pkgname}-${pkgver}.tar.bz2"
        'libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch'
        'libgcrypt-use-correct-def-file.patch'
        'libgcrypt-use-correct-asm-code-for-win64.patch')
md5sums=('993159b2924ae7b0e4eaff0743c2db35'
         '7bb247584a66a83677c618aec812ca9b'
         '531e089caca74b5daf130b7173c2a5c5'
         '8ea7a618d222f1301b0eaf596954bb51')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p0 -i "$srcdir"/libgcrypt-mingw64-use-correct-datatype-for-GetProcessWorkingSet.patch
  patch -p0 -i "$srcdir"/libgcrypt-use-correct-def-file.patch
  patch -p0 -i "$srcdir"/libgcrypt-use-correct-asm-code-for-win64.patch
}

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  unset CFLAGS LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --enable-static \
      --with-gpg-error-prefix=/usr/${_arch} \
      --enable-pubkey-ciphers='dsa elgamal rsa'
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_pkgname}-${pkgver}/build-$_arch"
    make DESTDIR="$pkgdir" install -j1
    rm -r "$pkgdir"/usr/${_arch}/share
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-all "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
