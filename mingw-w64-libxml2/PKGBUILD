# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libxml2
pkgver=2.9.1
pkgrel=3
arch=('any')
pkgdesc="XML parsing library, version 2 (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-gettext' 'mingw-w64-zlib' 'pkg-config')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip' 'staticlibs')
license=('LGPL')
url="http://www.xmlsoft.org/"
source=("http://xmlsoft.org/sources/libxml2-${pkgver}.tar.gz"
        "mingw32-libxml2-static-build-compile-fix.patch"
        "libxml2-no-test.patch")
md5sums=('9c0cfef285d5c4a5c80d00904ddab380'
         '0df377025082cd93cccbca547f048011'
         'ddd73e88dda6ae318267507409445b7d')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "${srcdir}/libxml2-${pkgver}"
  patch -Np0 -i "${srcdir}"/mingw32-libxml2-static-build-compile-fix.patch
  patch -Np1 -i "${srcdir}"/libxml2-no-test.patch
  sed -i "s| doc example | |g" Makefile.am
  sed -i "s|LIBXML_STATIC|_WIN32|g" include/libxml/xmlexports.h
  autoreconf -vfi
}

build()
{
  cd "${srcdir}/libxml2-${pkgver}"

  export LDFLAGS="-Wl,-no-undefined"
  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -DLIBXML_STATIC_FOR_DLL"

  # Static build
  for _arch in ${_architectures}; do
    mkdir -p build-static-${_arch} && pushd build-static-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build="${CHOST}" \
      --without-python \
      --with-modules \
      --enable-static \
      --disable-shared \
      --with-threads=win32
    make

    # deps symbols are not included:
    ${_arch}-ar x /usr/${_arch}/lib/libiconv.a
    ${_arch}-ar x /usr/${_arch}/lib/libz.a
    ${_arch}-ar x /usr/${_arch}/lib/libws2_32.a
    ${_arch}-ar cru .libs/libxml2.a *.o 
    ${_arch}-ranlib .libs/libxml2.a

    popd
  done

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"

  # Shared build
  for _arch in ${_architectures}; do
    mkdir -p build-shared-${_arch} && pushd build-shared-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build="${CHOST}" \
      --without-python \
      --with-modules \
      --disable-static \
      --enable-shared \
      --with-threads=win32
    make
    popd
  done
}

package()
{
  cd "${srcdir}/libxml2-${pkgver}"

  for _arch in ${_architectures}; do
    pushd build-shared-${_arch}
    make install DESTDIR="${pkgdir}"
    popd
    pushd build-static-${_arch}
    install -m 0644 .libs/libxml2.a "${pkgdir}"/usr/${_arch}/lib/
    popd

    ${_arch}-strip --strip-all "${pkgdir}"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-debug "${pkgdir}"/usr/${_arch}/lib/*.a

    # Remove documentation
    rm -rf "${pkgdir}"/usr/${_arch}/share
    
  done
}

