# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-libxml2
pkgver=2.9.1
pkgrel=2
arch=('any')
pkgdesc="XML parsing library, version 2 (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-gettext' 'mingw-w64-zlib' 'pkg-config')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip' 'staticlibs')
license=('LGPL')
url="http://www.xmlsoft.org/"
source=("http://xmlsoft.org/sources/libxml2-${pkgver}.tar.gz"
        "mingw32-libxml2-static-build-compile-fix.patch")
md5sums=('9c0cfef285d5c4a5c80d00904ddab380'
         '0df377025082cd93cccbca547f048011')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/libxml2-${pkgver}"

  patch -Np0 -i "${srcdir}"/mingw32-libxml2-static-build-compile-fix.patch

  export LDFLAGS="-Wl,-no-undefined"
  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4 -DLIBXML_STATIC_FOR_DLL"

  # Static build
  for _arch in ${_architectures}; do
    mkdir -p build-static-${_arch} && pushd build-static-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build="${CHOST}" \
      --without-python \
      --with-modules \
      --enable-static \
      --disable-shared \
      --with-threads=win32
    make
    popd
  done

  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"

  # Shared build
  for _arch in ${_architectures}; do
    mkdir -p build-shared-${_arch} && pushd build-shared-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build="${CHOST}" \
      --without-python \
      --with-modules \
      --disable-static \
      --enable-shared \
      --with-threads=win32
    make
    popd
  done
}

package()
{
  cd "${srcdir}/libxml2-${pkgver}"

  # First install shared
  for _arch in ${_architectures}; do
    pushd build-shared-${_arch}

    make install DESTDIR="${pkgdir}"

    ${_arch}-strip --strip-all "${pkgdir}"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll

    # Remove documentation
    rm -rf "${pkgdir}"/usr/${_arch}/share/man
    rm -rf "${pkgdir}"/usr/${_arch}/share/doc
    rm -rf "${pkgdir}"/usr/${_arch}/share/gtk-doc
    
    popd
  done

  # then manually install static
  for _arch in ${_architectures}; do
    pushd build-static-${_arch}

    install -m 0644 .libs/libxml2.a "${pkgdir}"/usr/${_arch}/lib/

    ${_arch}-strip --strip-debug "${pkgdir}"/usr/${_arch}/lib/*.a

    popd
  done
}
