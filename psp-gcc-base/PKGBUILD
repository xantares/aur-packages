# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=psp-gcc-base
pkgver=4.6.3
pkgrel=1
pkgdesc="The GNU Compiler Collection - C and C++ frontends (bootstrap) (psp)"
arch=(any)
url="http://gcc.gnu.org"
license=('GPL' 'LGPL' 'FDL' 'custom')
groups=('psp')
makedepends=('psp-binutils')
conflicts=('psp-gcc')
options=('!buildflags' '!strip' 'staticlibs')
source=("http://ftp.gnu.org/pub/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.bz2"
        "gcc-$pkgver-PSP.patch"
        "gcc-$pkgver-texinfofix.patch"
        "ftp://ftp.gmplib.org/pub/gmp-5.1.0/gmp-5.1.0.tar.bz2"
        "http://www.multiprecision.org/mpc/download/mpc-1.0.1.tar.gz"
        "http://www.mpfr.org/mpfr-3.1.1/mpfr-3.1.1.tar.bz2"
        "gcc-$pkgver-fix54638.patch")
md5sums=('773092fe5194353b02bb0110052a972e'
         '7dd38dff903ce384878208d5f81553c3'
         '0c857432912c838594d3ccfdf6103e8b'
         '362cf515aff8dc240958ce47418e4c78'
         'b32a2e1a3daa392372fbd586d1ed3679'
         'e90e0075bb1b5f626c6e31aaa9c64e3b'
         'f737d938d082eb7a0770a30bb9af602a') 

prepare ()
{
  cd "$srcdir/gcc-$pkgver"
  rm -f gcc/config/mips/allegrex.md gcc/config/mips/psp.h gcc/config/mips/t-allegrex
  patch -p1 -i "$srcdir"/gcc-$pkgver-PSP.patch
  patch -p1 -i "$srcdir"/gcc-$pkgver-texinfofix.patch
  patch -p2 -i "$srcdir"/gcc-$pkgver-fix54638.patch
  ln -sf "$srcdir"/gmp-5.1.0 gmp
  ln -sf "$srcdir"/mpc-1.0.1 mpc
  ln -sf "$srcdir"/mpfr-3.1.1 mpfr
}

build()
{
  cd "$srcdir/gcc-$pkgver"
  mkdir -p build-psp && pushd build-psp
  ../configure --prefix=/usr --target=psp \
    --enable-languages="c" --enable-lto --with-newlib --with-gmp --with-mpfr --without-headers --disable-libssp  
  make 
}

package()
{
  cd "$srcdir/gcc-$pkgver/build-psp"
  make install DESTDIR="$pkgdir"
  rm -r "$pkgdir"/usr/share
  rm "$pkgdir"/usr/lib/libiberty.a
}

