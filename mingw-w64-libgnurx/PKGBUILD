# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libgnurx
pkgver=2.5.1
pkgrel=3
arch=(any)
pkgdesc="libgnurx (mingw-w64)"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip' 'staticlibs')
license=('LGPL')
url="http://mingw.sourceforge.net/"
source=("http://download.sourceforge.net/mingw/Other/UserContributed/regex/mingw-regex-${pkgver}/mingw-libgnurx-${pkgver}-src.tar.gz"
        "mingw-w64-libgnurx-honor-destdir.patch")
md5sums=('35c8fed3101ca1f253e9b6b1966661f6'
         '2d87c59177adf5ec13dbcc02acc4a450')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"      

build()
{
  cd "$srcdir/mingw-libgnurx-${pkgver}"
  patch -Np0 -i ../mingw-w64-libgnurx-honor-destdir.patch
  for _arch in ${_architectures}; do
    unset LDFLAGS CPPFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch}
    make
    popd
  done
  
}

package()
{
  for _arch in ${_architectures}; do
    cd "$srcdir/mingw-libgnurx-${pkgver}/build-${_arch}"
    mkdir -p "$pkgdir"/usr/${_arch}/include
    make install DESTDIR="$pkgdir"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm -r "$pkgdir"/usr/${_arch}/man
  done
}
