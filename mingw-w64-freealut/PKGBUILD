# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=freealut
pkgname=mingw-w64-$_pkgname
pkgver=1.1.0
pkgrel=4
pkgdesc="OpenAL Utility Toolkit (mingw-w64)"
arch=('any')
url="http://www.openal.org"
license=("LGPL")
depends=('mingw-w64-crt' 'mingw-w64-openal')
makedepends=('mingw-w64-gcc' 'mingw-w64-openal')
options=('!libtool' '!buildflags' '!strip')
#source=("http://connect.creativelabs.com/openal/Downloads/ALUT/$_pkgname-$pkgver.tar.gz")
source=("ftp://ftp.spline.inf.fu-berlin.de/pub/gentoo/distfiles/$_pkgname-$pkgver.tar.gz")
md5sums=('e089b28a0267faabdb6c079ee173664a')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir/${_pkgname}-${pkgver}"
  sed -i "s|openal32|OpenAL32|g" configure.ac
  autoreconf -vfi
}

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_pkgname}-${pkgver}/build-$_arch"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

