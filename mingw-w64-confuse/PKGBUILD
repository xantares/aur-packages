# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=confuse
pkgname=mingw-w64-$_pkgname
pkgver=2.7
pkgrel=3
pkgdesc="C-library for parsing configuration files (mingw-w64)"
arch=('any')
url="http://www.nongnu.org/confuse"
license=('LGPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
source=("http://savannah.nongnu.org/download/$_pkgname/$_pkgname-$pkgver.tar.gz"
        'confuse-mingw.patch')
options=('!libtool' '!buildflags' '!strip')
md5sums=('45932fdeeccbb9ef4228f1c1a25e9c8f'
         '192a32be6bf6481d5cc173664204ea98')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 -i "${srcdir}"/confuse-mingw.patch
  autoreconf --verbose --install --symlink --force
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  for _arch in ${_architectures}; do
    unset LDFLAGS CPPFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-shared \
                 --disable-examples
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_pkgname}-${pkgver}/build-${_arch}"
    make install DESTDIR="$pkgdir"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}
