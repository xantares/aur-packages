# Maintainer: naelstrof <naelstrof@gmail.com>
# Contributor: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Paul Bienkowski <opatutlol@aol.com>
# Contributor: Christoph Zeiler <archNOSPAM_at_moonblade.dot.org>
pkgname=mingw-w64-bullet
pkgver=2.82
pkgrel=5
pkgdesc="A 3D Collision Detection and Rigid Body Dynamics Library for games and  animation (mingw-w64)"
arch=('any')
url="http://www.bulletphysics.com/Bullet/"
license=('custom:zlib')
depends=('mingw-w64-crt')
options=('!strip' '!buildflags' 'staticlibs')
optdepends=('bullet-docs: documentation')
makedepends=('mingw-w64-cmake')
source=("http://bullet.googlecode.com/files/bullet-${pkgver}-r2704.tgz")
md5sums=('70b3c8d202dee91a0854b4cbc88173e8')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd ${srcdir}/bullet-${pkgver}-r2704
  sed -i "s|#define SIMD_EPSILON      FLT_EPSILON|#define SIMD_EPSILON 1.19209290E-07F|g" src/LinearMath/btScalar.h
}

build() {
  cd ${srcdir}/bullet-${pkgver}-r2704
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DBUILD_SHARED_LIBS=1 \
      -DBUILD_DEMOS=0 \
      -DBUILD_EXTRAS=1 \
      -DINSTALL_LIBS=1 \
      -DUSE_GLUT=0 \
      -DCMAKE_CXX_FLAGS_RELEASE="-fpermissive" \
      -DINSTALL_EXTRA_LIBS=1 \
      -DCMAKE_BUILD_TYPE=Release \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/bullet-${pkgver}-r2704/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    install -d "$pkgdir"/usr/${_arch}/bin
    mv "${pkgdir}"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin/
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
