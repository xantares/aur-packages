pkgname=mingw-w64-ilmbase
pkgver=2.1.0
pkgrel=1
options=('!buildflags' '!strip' 'staticlibs')
depends=("mingw-w64-crt")
makedepends=("mingw-w64-gcc")
pkgdesc="Base libraries from ILM for OpenEXR (mingw-w64)"
arch=(any)
url="http://www.openexr.com"
license=('custom')
source=("http://download.savannah.nongnu.org/releases/openexr/ilmbase-$pkgver.tar.gz"
        'ilmbase-2.1.0-win32_threads.patch' 
      	'ilmbase-2.1.0_obsolete-macros.patch')
md5sums=('8ba2f608191ad020e50277d8a3ba0850'
         '9f8cab8a9e36316cfdc4eec056e26b55'
         '9582535a8c93094c14a3234657113dd0')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/ilmbase-$pkgver"
  # patches from fedora
  patch -p1 -i ../ilmbase-2.1.0-win32_threads.patch
  patch -p1 -i ../ilmbase-2.1.0_obsolete-macros.patch

  # no clue how to fix this one properly
  grep -lr LDBL_EPSILON Imath | xargs sed -i "s|LDBL_EPSILON|1.08420217248550443401e-19L|g"
  grep -lr DBL_EPSILON Imath | xargs sed -i "s|DBL_EPSILON|2.2204460492503131E-16|g"
  grep -lr FLT_EPSILON Imath 3rdparty | xargs sed -i "s|FLT_EPSILON|1.19209290E-07F|g"
}

build() {
  cd "${srcdir}/ilmbase-$pkgver"
  ./bootstrap
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch}
    g++ -o Half/eLut ../Half/eLut.cpp
    g++ -o Half/toFloat ../Half/toFloat.cpp
    ./Half/eLut > ./Half/eLut.h
    ./Half/toFloat > ./Half/toFloat.h
    make
    popd
  done
}


package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/ilmbase-$pkgver/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a  
  done
}

