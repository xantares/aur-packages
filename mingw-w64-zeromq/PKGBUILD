# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=zeromq
pkgname=mingw-w64-$_pkgname
pkgver=4.0.1
pkgrel=1
pkgdesc="Fast messaging system built on sockets, C and C++ bindings. aka 0MQ, ZMQ. (mingw-w64)"
arch=('any')
url="http://www.zeromq.org"
license=("LGPL")
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
depends=('mingw-w64-crt')
options=('!libtool' '!strip' '!buildflags')
source=("http://download.zeromq.org//$_pkgname-$pkgver.tar.gz")
md5sums=('e738c310d135e20fa48c5b348da18e22')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  sed "s|\$(top_srcdir)/src/libzmq.la|\$(top_builddir)/src/libzmq.la|g" -i tools/Makefile.am
  autoreconf -vfi
  for _arch in ${_architectures}; do
    unset LDFLAGS
    export CFLAGS="-O2 -pipe"
    export CXXFLAGS="$CFLAGS"
    export CPPFLAGS="$CPPFLAGS -D_REENTRANT"
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_pkgname}-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
