# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-readline
_basever=6.2
_patchlevel=004
pkgver=$_basever.$_patchlevel
pkgrel=3
pkgdesc="MinGW port of readline for editing typed command lines (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-termcap')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!buildflags' '!strip')
license=('GPL')
url="http://cnswww.cns.cwru.edu/php/chet/readline/rltop.html"
source=("http://ftp.gnu.org/gnu/readline/readline-${_basever}.tar.gz"{,.sig})
if [ $_patchlevel -gt 00 ]; then
    for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
        source=(${source[@]} http://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//./}-$(printf "%03d" $p){,.sig})
    done
fi

md5sums=('67948acb2ca081f23359d0256e9a271c'
         '928f7d248320a65e43c2dc427e99582b'
         '83287d52a482f790dfb30ec0a8746669'
         '8e6a51e2e0e6e45a82752e3692c111ac'
         '0665020ea118e8434bd145fb71f452cc'
         '285361ca6d48c51ae2428157e174e812'
         'c9d5d79718856e711667dede87cb7622'
         '4437205bb1462f5f32e4812b8292c675'
         'c08e787f50579ce301075c523fa660a4'
         '7e39cad1d349b8ae789e4fc33dbb235f')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/readline-${_basever}"

  for (( p=1; p<=$((10#${_patchlevel})); p++ )); do
    msg "applying patch readline${_basever//./}-$(printf "%03d" $p)"
    patch -Np0 -i $srcdir/readline${_basever//./}-$(printf "%03d" $p)
  done

  # Remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf

  # pushd examples
  # rm -f rlfe/configure
  # iconv -f iso8859-1 -t utf8 -o rl-fgets.c{_,}
  # touch -r rl-fgets.c{,_}
  # mv -f rl-fgets.c{_,}
  # popd

  unset LDFLAGS
  export CFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"
  export CXXFLAGS="-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4"

  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --target=${_arch} \
      --build=${CARCH} \
      --enable-shared
    make SHLIB_LIBS="-ltermcap"
    popd
  done
}

package() {
  for _arch in ${_architectures} ; do
    cd "${srcdir}/readline-${_basever}/build-${_arch}"
    make install DESTDIR="${pkgdir}"
    rm -r "${pkgdir}"/usr/${_arch}/share/
    ${_arch}-strip --strip-unneeded "${pkgdir}"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-debug "${pkgdir}"/usr/${_arch}/lib/*.a
  done
}

# vim:set ts=2 sw=2 et:
