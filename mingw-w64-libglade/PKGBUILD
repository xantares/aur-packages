# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=libglade
pkgname=mingw-w64-$_pkgname
pkgver=2.6.4
pkgrel=3
pkgdesc="Allows you to load glade interface files in a program at runtime (mingw-w64)"
arch=(any)
license=('LGPL')
depends=('mingw-w64-crt' 'pkgconfig')
makedepends=('mingw-w64-gcc' 'pkgconfig' 'mingw-w64-gtk2' 'mingw-w64-fontconfig' 'mingw-w64-pango' 'mingw-w64-gettext' 'mingw-w64-libxml2')
options=('!libtool' '!strip' '!buildflags')
source=("http://ftp.gnome.org/pub/gnome/sources/libglade/2.6/libglade-${pkgver}.tar.bz2"
        'libglade-2.0.1-nowarning.patch'
        'libglade-secondary.patch'
        'libglade-link-against-gmodule.patch')
url="http://www.gnome.org"
md5sums=('d1776b40f4e166b5e9c107f1c8fe4139'
         '4f879c0ce550004905aa0fb24f25c353'
         '833d0feb0ffb01d99577b7ab92dbea89'
         'eb42bfc429adf52f4e72c959e8c50385')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir"/libglade-2.0.1-nowarning.patch
  patch -Np1 -i "$srcdir"/libglade-secondary.patch
  patch -Np0 -i "$srcdir"/libglade-link-against-gmodule.patch
}

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  autoreconf -if
  unset LDFLAGS CFLAGS 
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    export PKG_CONFIG_LIBDIR=/usr/${_arch}/lib/pkgconfig/
    ../configure --prefix=/usr/${_arch} --host=${_arch}
    cp ../glade/glade.def glade/
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/${_pkgname}-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -m755 libglade-convert "$pkgdir"/usr/${_arch}/bin/
    rm -r "$pkgdir"/usr/${_arch}/share/gtk-doc
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
