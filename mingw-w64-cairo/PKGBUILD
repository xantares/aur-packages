# Contributor: Xiao-Long Chen <chenxiaolong@cxl.epac.to>

pkgname=mingw-w64-cairo
pkgver=1.12.16
pkgrel=6
pkgdesc="Cairo vector graphics library (mingw-w64)"
arch=(any)
url="http://cairographics.org"
license=("LGPL, MPL")
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config' 'mingw-w64-glib2')
depends=('mingw-w64-crt' 'mingw-w64-fontconfig' 'mingw-w64-pixman')
optdepends=("mingw-w64-glib2: libcairo-gobject")
options=('!strip' '!buildflags' 'staticlibs')
source=("http://cairographics.org/releases/cairo-${pkgver}.tar.xz"
        'cairo-1.12.16-lto-optional.patch'
        '0003-build-sys-do-not-try-to-build-util-sphinx-on-Windows.patch')
sha1sums=('4f6e337d5d3edd7ea79d1426f575331552b003ec'
          '80883e44a57942762995aea2f136701b1fa54568'
          '7e5cf0828aea1c6cd33364f377c7a5260d5c4ea9')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd cairo-${pkgver}
  patch -p1 -i "${srcdir}"/cairo-1.12.16-lto-optional.patch
  patch -p1 -i "${srcdir}"/0003-build-sys-do-not-try-to-build-util-sphinx-on-Windows.patch
}

build() {
  cd cairo-${pkgver}
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    export CFLAGS="-O2 -mms-bitfields"
    unset LDFLAGS
    "${srcdir}"/${pkgname#mingw-w64-}-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-win32 \
      --enable-win32-font \
      --enable-png \
      --enable-shared \
      --enable-static \
      --enable-gobject \
      --enable-tee \
      --disable-xlib \
      --disable-xcb \
      --disable-lto \
      --enable-fc \
      --enable-ft
    sed -i "s/deplibs_check_method=.*/deplibs_check_method=pass_all/g" libtool
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/cairo-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir/usr/${_arch}/share"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
