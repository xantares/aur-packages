pkgname=mingw-w64-coin-clp
pkgver=1.15.6
pkgrel=1
pkgdesc="Clp (Coin-or linear programming) is an open-source linear programming solver written in C++ (mingw-w64)"
arch=('any')
url="https://projects.coin-or.org/Clp"
license=('EPL')
groups=('coin-or')
depends=('mingw-w64-crt' 'mingw-w64-coin-osi' 'mingw-w64-zlib' 'mingw-w64-bzip2' 'mingw-w64-lapack')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!buildflags' '!strip')
source=(http://www.coin-or.org/download/source/Clp/Clp-$pkgver.tgz)
sha512sums=('a0da0cc5dcdd409c67cb67dd0f126a3d96bf59851543e7e08adbdbf01012a25905b58ded66aac50b06462ee3a147bed912740c00681417f8c228992a917748db')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd Clp-$pkgver
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    COIN_SKIP_PROJECTS="Sample" \
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
              --with-osi-lib="$(${_arch}-pkg-config --libs osi)" \
              --with-osi-incdir="/usr/${_arch}/include/coin/" \
              --with-coinutils-lib="$(${_arch}-pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/${_arch}/include/coin/" \
              --enable-shared -C
    make
    popd
  done
}


package() {
  for _arch in ${_architectures}; do
    cd "$srcdir"/Clp-$pkgver/build-${_arch}
    PKG_CONFIG_PATH_CUSTOM="$pkgdir"/usr/${_arch}/lib/pkgconfig/ \
    make DESTDIR="$pkgdir"/ install
    rm -r "$pkgdir"/usr/${_arch}/share
    #${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done

}
