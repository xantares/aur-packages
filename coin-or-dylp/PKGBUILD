# Contributor: spider-mario <spidermario@free.fr>
# Contributor: Daniel Ehlers <danielehlers@mindeye.net>
pkgname=coin-or-dylp
pkgver=1.9.5
pkgrel=1
pkgdesc="COIN-OR Linear programming solver using of the dynamic simplex algorithm"
arch=('i686' 'x86_64')
url="https://projects.coin-or.org/DyLP"
license=('EPL')
groups=('coin-or')
depends=('coin-or-osi')
source=("http://www.coin-or.org/download/source/DyLP/DyLP-${pkgver}.zip")
sha1sums=('1740cb9a3d62a6a471168d3e0aafd251ecfe9faa')

build() {
  cd DyLP-$pkgver
  COIN_SKIP_PROJECTS="Sample" \
  LDFLAGS="$LDFLAGS -Wl,-lm" \
  ./configure --prefix=/usr \
              --with-osi-lib="$(pkg-config --libs osi)" \
              --with-osi-incdir="/usr/include/coin/" \
              --with-coinutils-lib="$(pkg-config --libs coinutils)" \
              --with-coinutils-incdir="/usr/include/coin/" -C
  make
}

# check() {
#   cd DyLP-$pkgver
# #   make test
# }

package() {
  cd DyLP-$pkgver
  PKG_CONFIG_LIBDIR="$pkgdir"/usr/lib/pkgconfig/ \
  make DESTDIR="$pkgdir"/ install
}
