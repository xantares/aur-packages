
pkgname=mingw-w64-ode
pkgver=0.13
pkgrel=1
pkgdesc="An open source, high performance library for simulating rigid body dynamics (mingw-w64)"
arch=(any)
url="http://www.ode.org"
license=("BSD, LGPL")
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
depends=('mingw-w64-crt')
options=('staticlibs' '!strip' '!buildflags')
source=("http://downloads.sourceforge.net/sourceforge/opende/ode-${pkgver}.tar.bz2")
md5sums=('04b32c9645c147e18caff7a597a19f84')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd ${srcdir}/ode-${pkgver}
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-shared \
      --enable-static \
      --enable-double-precision \
      --enable-libccd
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/ode-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
