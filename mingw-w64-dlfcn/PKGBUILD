# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-dlfcn
pkgver=r19
pkgrel=2
pkgdesc="A wrapper for dlfcn to the Win32 API (mingw-w64)"
arch=(any)
url="http://code.google.com/p/dlfcn-win32"
license=('LGPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip')
source=("http://dlfcn-win32.googlecode.com/files/dlfcn-win32-${pkgver}.tar.bz2")
md5sums=('6e321b36ee289179777bab07dcfe5510')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"      

build()
{
  cd "$srcdir/dlfcn-win32-$pkgver"
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    unset LDFLAGS CPPFLAGS
    ../configure \
      --prefix=/usr/${_arch} \
      --libdir=/usr/${_arch}/lib \
      --incdir=/usr/${_arch}/include \
      --disable-stripping \
      --enable-shared \
      --disable-msvc \
      --cc=${_arch}-gcc \
      --cross-prefix=/usr/${_arch}/bin/
    cp ../Makefile ../dlfcn.c ../dlfcn.h .
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/dlfcn-win32-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip -x -g "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
