
pkgname=mingw-w64-openexr
pkgver=2.1.0
pkgrel=1
pkgdesc="An high dynamic-range image file format library (mingw-w64)"
url="http://www.openexr.com/"
arch=(any)
license=('BSD')
depends=('mingw-w64-crt' 'mingw-w64-zlib' 'mingw-w64-ilmbase')
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config')
options=('staticlibs' '!buildflags' '!strip')
source=("http://download.savannah.nongnu.org/releases/openexr/openexr-${pkgver}.tar.gz"
       openexr-2.1.0-headers.patch
       openexr-2.1.0_bb44ExpLogTable.patch
       openexr-2.1.0_aligned-malloc.patch
       openexr-2.1.0_cast.patch
       openexr_obsolete-macros.patch)
md5sums=('33735d37d2ee01c6d8fbd0df94fb8b43'
         'd87df3d057dd8d902c61a3b88f05fd0d'
         '4798df032716e33a2b227542319fa8f5'
         '09df5525cf2d53d013304dd5e533ce0f'
         '4ec9ab6cc823dc9fcc28cfc85a761546'
         '2f20a165ea31e7638a37809dd5feefb1')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/openexr-${pkgver}"
  # fedora patches
  patch -p1 -i "${srcdir}"/openexr-2.1.0-headers.patch
  patch -p1 -i "${srcdir}"/openexr-2.1.0_bb44ExpLogTable.patch
  patch -p1 -i "${srcdir}"/openexr-2.1.0_aligned-malloc.patch
  patch -p1 -i "${srcdir}"/openexr-2.1.0_cast.patch
  patch -p1 -i "${srcdir}"/openexr_obsolete-macros.patch
}

build() {
  cd "${srcdir}/openexr-${pkgver}"
  ./bootstrap
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --with-pkg-config=/usr/bin/${_arch}-pkg-config \
                 --disable-ilmbasetest
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/openexr-${pkgver}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    rm -r "$pkgdir"/usr/${_arch}/share/doc
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
