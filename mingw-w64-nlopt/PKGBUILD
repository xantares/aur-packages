# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-nlopt
pkgver=2.3
pkgrel=1
pkgdesc="a non-linear optimization package (mingw-w64)"
arch=(any)
url="http://ab-initio.mit.edu/wiki/index.php/NLopt"
license=('LGPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!strip')
source=("http://ab-initio.mit.edu/nlopt/nlopt-$pkgver.tar.gz")
md5sums=('811a9f1c7a7f879c7d7b4caa059eb8d6')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"


build() {
  cd $srcdir/nlopt-$pkgver
  sed -i "s|top_builddir)/api|top_srcdir)/api|g" swig/Makefile.am
  autoreconf --verbose --install --symlink --force
  
  for _arch in ${_architectures}; do
    unset LDFLAGS CPPFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --enable-shared \
                 --without-python \
                 --without-guile \
                 --without-octave
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd ${srcdir}/nlopt-${pkgver}/build-${_arch}
    make install DESTDIR=${pkgdir}
    rm $pkgdir/usr/${_arch}/lib/*.la
    ${_arch}-strip -x -g $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a    
  done
}
