# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libftdi
pkgver=1.1
pkgrel=1
license=('GPL2' 'LGPL2.1')
arch=(any)
pkgdesc='Library to talk to FTDI chips (mingw-w64)'
url='http://www.intra2net.com/en/developer/libftdi/'
depends=('mingw-w64-crt' 'mingw-w64-libusb')
optdepends=('mingw-w64-python: python bindings')
makedepends=('mingw-w64-confuse' 'mingw-w64-cmake' 'mingw-w64-boost' 'mingw-w64-python' 'wine')
options=('!strip' '!buildflags' 'staticlibs')
source=(http://www.intra2net.com/en/developer/libftdi/download/libftdi1-$pkgver.tar.bz2)
sha1sums=('f05ade5614aa31e64f91a30ce3782f7ca3704d18')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir"/libftdi1-1.1
  sed -i "s|COMMAND \${PYTHON_EXECUTABLE}|COMMAND wine \${PYTHON_EXECUTABLE}|g" python/CMakeLists.txt
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    cp /usr/${_arch}/bin/*.dll .
    ${_arch}-cmake \
      -DEXAMPLES=OFF \
      -DPYTHON_EXECUTABLE=/usr/${_arch}/bin/python3.exe \
      ..
    cp /usr/${_arch}/bin/*.dll python
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/libftdi1-1.1/build-${_arch}"
    make install DESTDIR="$pkgdir"
    rm -r "$pkgdir"/usr/${_arch}/share
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    ${_arch}-strip "$pkgdir"/usr/${_arch}/bin/*.exe
  done
}

