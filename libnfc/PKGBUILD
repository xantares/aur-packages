# Contributor: Pascal E. <archlinux at hardfalcon dot net>
# Contributor: Andrej Gelenberg <andrej.gelenberg at udo dot edu>
pkgname=libnfc
pkgver=1.7.1
pkgrel=1
pkgdesc="libre NFC SDK and Programmers API"
arch=('i686' 'x86_64' 'arm')
url=("https://code.google.com/p/libnfc/")
license=('LGPL3')
depends=('libusb-compat' 'pcsclite')
makedepends=('cmake')
source=("http://dl.bintray.com/nfc-tools/sources/libnfc-${pkgver}.tar.bz2")
sha1sums=('d77c1408f5e38f24d69778b2ff82f68b2d7bc111')

prepare()
{
  cd "$srcdir/${pkgname}-${pkgver}"
  sed -i "s|PROPERTIES SOVERSION 0|PROPERTIES SOVERSION 5 VERSION 5.0.1|g" libnfc/CMakeLists.txt
}

build()
{
  cd "$srcdir/${pkgname}-${pkgver}"
  cmake -DCMAKE_INSTALL_PREFIX=/usr .
  make
}

package()
{
  cd "$srcdir/${pkgname}-${pkgver}"
  make DESTDIR="$pkgdir" install
  install -Dm 644 contrib/udev/42-pn53x.rules "$pkgdir"/etc/udev/rules.d/42-pn53x.rules
}

