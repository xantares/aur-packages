pkgname=mingw-w64-libmad
pkgver=0.15.1b
pkgrel=4
pkgdesc="A high-quality MPEG audio decoder (mingw-w64)"
arch=(any)
url="http://www.underbit.com/products/mad"
license=("GPL")
makedepends=(mingw-w64-gcc mingw-w64-pkg-config)
depends=(mingw-w64-crt)
options=(staticlibs !strip !buildflags)
source=("http://downloads.sourceforge.net/mad/libmad/${pkgver}/libmad-${pkgver}.tar.gz"
"optimize.diff"
"frame_length.diff"
"libmad.patch")
md5sums=('1be543bc30c56fb6bea1d7bf6a64e66c'
         '787115a740a7b2675acb385cf2f11f6a'
         '3329b032118a68e18908197e65b3198d'
         'fbcca0267a423bae45389666f4cb04d5')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir/libmad-$pkgver"
  rm -f mad.pc.in
  patch -p1 -i "${srcdir}"/libmad.patch
  patch -p1 -i "${srcdir}"/frame_length.diff
  patch -p1 -i "${srcdir}"/optimize.diff
}

build() {
  cd "$srcdir/libmad-$pkgver"
  for _arch in ${_architectures}; do
    if test "${_arch}" = "x86_64-w64-mingw32"; then
      march=x86-64
    else
      march=i686
    fi

    CFLAGS="-march=$march -mtune=generic -O2 -s -ftree-vectorize -ftree-vectorizer-verbose=1"
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-static \
      --disable-debugging
    make
    ${_arch}-gcc $CFLAGS -shared .libs/*.o -o .libs/libmad-0.dll -Xlinker --out-implib -Xlinker .libs/libmad.dll.a
    popd
  done 
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/libmad-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -Dm644 .libs/libmad-0.dll "$pkgdir"/usr/${_arch}/bin/libmad-0.dll
    install -m644 .libs/libmad.dll.a "$pkgdir"/usr/${_arch}/lib/libmad.dll.a
    #${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    #${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
