# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=icu
pkgname=mingw-w64-$_pkgname
pkgver=50.1.2
pkgrel=1
pkgdesc="International Components for Unicode library (mingw-w64)"
arch=('any')
url="http://www.icu-project.org/"
license=('custom:"icu"')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!buildflags' '!libtool' '!strip')
source=("http://download.icu-project.org/files/${_pkgname}4c/${pkgver}/${_pkgname}4c-${pkgver//./_}-src.tgz"
        'icu4c-4_6_1-crossbuild.patch'
icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch
icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch)


md5sums=('SKIP' 'SKIP' 'SKIP' 'SKIP')
#'072e501b87065f3a0ca888f1b5165709'
#'SKIP' 'SKIP' 'SKIP')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/$_pkgname"

  # http://pkgs.fedoraproject.org/cgit/mingw-icu.git
  patch -p1 -i "$srcdir"/icu4c-4_6_1-crossbuild.patch
  patch -p1 -i "$srcdir"/icu4c-4_6_1-use-correct-genccode-assembly-on-win32-and-win64.patch
  patch -p1 -i "$srcdir"/icu4c-4_6_1-mingw-w64-mkdir-compatibility.patch

  cd source
  autoreconf -vfi
}

build() {
  cd "$srcdir/$_pkgname/source"
  mkdir -p nativebuild && pushd nativebuild
  CFLAGS=-fno-stack-protector
  ../configure --enable-static --disable-shared
  make
  popd
  for _arch in ${_architectures}; do
    unset CFLAGS LDFLAGS
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --with-cross-build=$PWD/../nativebuild \
      --with-data-packaging=library \
      --enable-static
    make
    popd
  done
}

package() {
  cd ${srcdir}/$_pkgname/source
  for _arch in ${_architectures}; do
    cd "$srcdir/$_pkgname/source/build-${_arch}"
    make install DESTDIR="$pkgdir"
    rm -rf "$pkgdir"/usr/${_arch}/share
    rm "$pkgdir"/usr/${_arch}/bin/*.exe
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
#     ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
