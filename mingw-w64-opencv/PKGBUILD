pkgname=mingw-w64-opencv
pkgver=2.4.9
pkgrel=1
pkgdesc="Open Source Computer Vision Library (mingw-w64)"
arch=('any')
license=('BSD')
url="http://opencv.org/"
options=('!buildflags' 'staticlibs' '!strip')
depends=('mingw-w64-crt' 'mingw-w64-jasper' 'mingw-w64-libpng' 'mingw-w64-libjpeg-turbo' 'mingw-w64-libtiff' 'mingw-w64-zlib' 'mingw-w64-openexr')
makedepends=('mingw-w64-cmake')
source=("http://downloads.sourceforge.net/opencvlibrary/opencv-$pkgver.zip"
        mingw-opencv-2.4.2-systemlibs.patch)
md5sums=('7f958389e71c77abdf5efe1da988b80c'
         '86bc67c211c695dea9fe5adf3ef9d699')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/opencv-$pkgver"

  # fedora patch
  # https://nucleo.fedorapeople.org/rpms/mingw-opencv/
  patch -p1 -i "${srcdir}"/mingw-opencv-2.4.2-systemlibs.patch

  # no clue how to fix this one properly
  # https://bugzilla.redhat.com/show_bug.cgi?id=843436
  grep -lr LDBL_EPSILON modules 3rdparty | xargs sed -i "s| LDBL_EPSILON| 1.08420217248550443401e-19L|g"
  grep -lr DBL_EPSILON modules 3rdparty | xargs sed -i "s| DBL_EPSILON| 2.2204460492503131E-16|g"
  grep -lr DBL_EPSILON modules 3rdparty | xargs sed -i "s|(DBL_EPSILON|(2.2204460492503131E-16|g"
  grep -lr DBL_EPSILON modules 3rdparty | xargs sed -i "s|)DBL_EPSILON|)2.2204460492503131E-16|g"
  grep -lr DBL_EPSILON modules 3rdparty | xargs sed -i "s|+DBL_EPSILON|+2.2204460492503131E-16|g"
  grep -lr FLT_EPSILON modules 3rdparty | xargs sed -i "s| FLT_EPSILON| 1.19209290E-07F|g"
}

build() {
  cd "$srcdir/opencv-$pkgver"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_SKIP_RPATH=ON \
      -DENABLE_SSE=0 \
      -DENABLE_SSE2=0 \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTS=OFF \
      -DBUILD_DOCS=OFF \
      -DWITH_FFMPEG=OFF \
      -DINSTALL_C_EXAMPLES=0 \
      -DINSTALL_PYTHON_EXAMPLES=0 \
      -DBUILD_ZLIB=OFF \
      -DBUILD_TIFF=OFF \
      -DBUILD_JASPER=OFF \
      -DBUILD_JPEG=OFF \
      -DBUILD_PNG=OFF \
      -DBUILD_OPENEXR=OFF \
      ..
    make
    popd
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -DCMAKE_SKIP_RPATH=ON \
      -DENABLE_SSE=0 \
      -DENABLE_SSE2=0 \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_TESTS=OFF \
      -DBUILD_DOCS=OFF \
      -DWITH_FFMPEG=OFF \
      -DINSTALL_C_EXAMPLES=0 \
      -DINSTALL_PYTHON_EXAMPLES=0 \
      -DBUILD_ZLIB=OFF \
      -DBUILD_TIFF=OFF \
      -DBUILD_JASPER=OFF \
      -DBUILD_JPEG=OFF \
      -DBUILD_PNG=OFF \
      -DBUILD_OPENEXR=OFF \
      -DBUILD_SHARED_LIBS=OFF \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/opencv-$pkgver/build-${_arch}-static"
    make DESTDIR="$pkgdir" install
    cd "$srcdir/opencv-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    install -d "$pkgdir"/usr/${_arch}/bin
    install -m755 "$pkgdir"/usr/${_arch}/x64/mingw/bin/*.dll "$pkgdir"/usr/${_arch}/bin
    install -d "$pkgdir"/usr/${_arch}/lib/
    install -m644 "$pkgdir"/usr/${_arch}/x64/mingw/lib/*.dll.a "$pkgdir"/usr/${_arch}/x64/mingw/staticlib/*.a "$pkgdir"/usr/${_arch}/lib
    install -d "$pkgdir"/usr/${_arch}/lib/cmake/opencv
    install -m644 "$pkgdir"/usr/${_arch}/*.cmake "$pkgdir"/usr/${_arch}/lib/cmake/opencv
    rm -r "$pkgdir"/usr/${_arch}/x64
    rm "$pkgdir"/usr/${_arch}/LICENSE
    rm "$pkgdir"/usr/${_arch}/*.cmake
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

