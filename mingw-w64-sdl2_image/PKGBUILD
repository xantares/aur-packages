pkgname=mingw-w64-sdl2_image
pkgver=2.0.0
pkgrel=8
pkgdesc="SDL2 image libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org"
license=("MIT")
makedepends=(mingw-w64-gcc
mingw-w64-pkg-config
mingw-w64-libpng
mingw-w64-libwebp
mingw-w64-libjpeg-turbo
mingw-w64-libtiff)
depends=(mingw-w64-crt mingw-w64-sdl2)
options=(staticlibs !strip !buildflags)
optdepends=("mingw-w64-libjpeg-turbo: JPEG support"
"mingw-w64-libpng: PNG support"
"mingw-w64-libtiff: TIFF support"
"mingw-w64-libwebp: WebP support")
source=("http://www.libsdl.org/projects/SDL_image/release/SDL2_image-$pkgver.tar.gz")
md5sums=('fb399c8386fb3248f1b33cfe81bdf92b')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/SDL2_image-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
