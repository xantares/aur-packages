pkgname=mingw-w64-hdf5
pkgver=1.8.12
pkgrel=1
arch=('any')
pkgdesc="General purpose library and file format for storing scientific data (mingw-w64)"
url="http://www.hdfgroup.org/HDF5/"
license=('custom')
depends=('mingw-w64-crt' 'mingw-w64-zlib')
makedepends=('mingw-w64-cmake' 'binfmt-wine')
options=('!strip' '!buildflags' 'staticlibs')
source=("ftp://ftp.hdfgroup.org/HDF5/current/src/hdf5-${pkgver/_/-}.tar.bz2"
	'hdf5-link-ws2_32-mingw.patch'
	'hdf5-default-import-suffix.patch')
sha1sums=('8414ca0e6ff7d08e423955960d641ec5f309a55f'
	  '6568e9787b00c706dc07a9f93d4c64f383ccc76e'
	  'a1b6adb2b01bdaa23ec8c4daaee1b5afa63dec96')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir/hdf5-${pkgver/_/-}"
  patch -p1 -i "$srcdir"/hdf5-link-ws2_32-mingw.patch
  patch -p1 -i "$srcdir"/hdf5-default-import-suffix.patch
  
  # rename libhdf5.dll to libhdf5-1.dll
  echo "IF (BUILD_SHARED_LIBS)" >> src/CMakeLists.txt
  echo "SET_TARGET_PROPERTIES (\${HDF5_LIB_TARGET} PROPERTIES SUFFIX -1.dll)">> src/CMakeLists.txt
  echo "ENDIF ()" >> src/CMakeLists.txt
}

build() {
  cd "$srcdir/hdf5-${pkgver/_/-}"
  for _arch in $_architectures; do
    unset LDFLAGS
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -DBUILD_SHARED_LIBS=OFF \
      -DHAVE_IOEO_EXITCODE=1 \
      -DHDF5_PRINTF_LL_TEST=1 \
      -DH5_LDOUBLE_TO_INTEGER_WORKS=1 \
      -DH5_ULONG_TO_FLOAT_ACCURATE=1 \
      -DH5_LDOUBLE_TO_UINT_ACCURATE=1 \
      -DH5_FP_TO_ULLONG_ACCURATE=1 \
      -DH5_ULLONG_TO_LDOUBLE_PRECISION=1 \
      -DH5_FP_TO_INTEGER_OVERFLOW_WORKS=1 \
      -DH5_LDOUBLE_TO_LLONG_ACCURATE=1 \
      -DH5_LLONG_TO_LDOUBLE_CORRECT=1 \
      -DH5_NO_ALIGNMENT_RESTRICTIONS=1 \
      -DHDF5_PRINTF_LL_TEST_RUN=1 \
      -DHDF5_PRINTF_LL_TEST_COMPILE=1 \
      -DHDF5_PRINTF_LL_TEST_RUN__TRYRUN_OUTPUT=1 \
      ..
    make
    popd
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DHAVE_IOEO_EXITCODE=1 \
      -DHDF5_PRINTF_LL_TEST=1 \
      -DH5_LDOUBLE_TO_INTEGER_WORKS=1 \
      -DH5_ULONG_TO_FLOAT_ACCURATE=1 \
      -DH5_LDOUBLE_TO_UINT_ACCURATE=1 \
      -DH5_FP_TO_ULLONG_ACCURATE=1 \
      -DH5_ULLONG_TO_LDOUBLE_PRECISION=1 \
      -DH5_FP_TO_INTEGER_OVERFLOW_WORKS=1 \
      -DH5_LDOUBLE_TO_LLONG_ACCURATE=1 \
      -DH5_LLONG_TO_LDOUBLE_CORRECT=1 \
      -DH5_NO_ALIGNMENT_RESTRICTIONS=1 \
      -DHDF5_PRINTF_LL_TEST_RUN=1 \
      -DHDF5_PRINTF_LL_TEST_COMPILE=1 \
      -DHDF5_PRINTF_LL_TEST_RUN__TRYRUN_OUTPUT=1 \
      ..
    make
    popd
  done
}

package() {
  for _arch in $_architectures; do
    cd "$srcdir/hdf5-${pkgver/_/-}/build-${_arch}-static"
    make DESTDIR="${pkgdir}" install
    cd "$srcdir/hdf5-${pkgver/_/-}/build-${_arch}"
    make DESTDIR="${pkgdir}" install
    rm "$pkgdir"/usr/${_arch}/*.txt
    rm "$pkgdir"/usr/${_arch}/{ACKNOWLEDGMENTS,COPYING}
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

