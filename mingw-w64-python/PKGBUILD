# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=python
pkgname=mingw-w64-$_pkgname
pkgver=3.3.0
pkgrel=1
_pybasever=3.3
pkgdesc="Next generation of the python high-level scripting language (mingw-w64)"
arch=('any')
license=('custom')
url="http://www.python.org/"
depends=('mingw-w64-crt' 'mingw-w64-expat' 'mingw-w64-bzip2' 'mingw-w64-gdbm' 'mingw-w64-openssl' 'mingw-w64-libffi' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc' 'mingw-w64-pkg-config' 'binfmt-wine')
options=('!makeflags' '!libtool' '!buildflags' '!strip')
source=("http://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
        'crucifixion-freedom-python.tar.gz')
sha1sums=('SKIP'
# '87009d0c156c6e1354dfec5c98c328cae93950ad'
          '94deaebee05d1b2f4435ef67747a9558f484d555')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/Python-${pkgver}"
  
  # these are created by patches
  rm -f Misc/python-config.sh.in setup_info.in Python/fileblocks.c Modules/_ctypes/libffi_msvc/win32.S
  
  patch -Np1 -i "${srcdir}"/3.3.0/0000-add-python-config-sh.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0005-cross-fixes.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0030-py3k-20121004-MINGW.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0031-py3k-20121004-MINGW-removal-of-pthread-patch.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0032-py3k-20121004-MINGW-ntthreads.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0033-py3k-mingw-ntthreads-vs-pthreads.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0034-py3k-20121004-MINGW-removal-of-libffi-patch.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0035-mingw-system-libffi.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0045-mingw-use-posix-getpath.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0050-mingw-sysconfig-like-posix.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0055-mingw-_winapi_as_builtin_for_Popen_in_cygwinccompiler.patch
  sed -i "s|__USE_MINGW_ANSI_STDIO|__MINGW32__|g" "${srcdir}"/3.3.0/0060-mingw-x86_64-size_t-format-specifier-pid_t.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0060-mingw-x86_64-size_t-format-specifier-pid_t.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0065-cross-dont-add-multiarch-paths-if-cross-compiling.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0070-mingw-use-backslashes-in-compileall-py.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0075-msys-convert_path-fix-and-root-hack.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0080-mingw-hack-around-double-copy-scripts-issue.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0085-allow-static-tcltk.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0090-CROSS-avoid-ncursesw-include-path-hack.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0091-CROSS-properly-detect-WINDOW-_flags-for-different-nc.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0092-mingw-pdcurses_ISPAD.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0095-no-xxmodule-for-PYDEBUG.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0100-grammar-fixes.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0105-builddir-fixes.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0110-msys-monkeypatch-os-system-via-sh-exe.patch
  patch -Np1 -i "${srcdir}"/3.3.0/0115-msys-replace-slashes-used-in-io-redirection.patch

  autoreconf -vfi
  
  # FS#23997
  sed -i -e "s|^#.* /usr/local/bin/python|#!/usr/bin/python|" Lib/cgi.py

  # Ensure that we are using the system copy of various libraries (expat, zlib and libffi),
  # rather than copies shipped in the tarball
  rm -r Modules/expat
  rm -r Modules/zlib
  rm -r Modules/_ctypes/{darwin,libffi}*
#   rm -f Lib/test/nullbytecert.pem 
}

build() {
  cd "${srcdir}/Python-${pkgver}"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    cp /usr/${_arch}/bin/libwinpthread*.dll .
    export LIBFFI_INCLUDEDIR=`${_arch}-pkg-config libffi --cflags-only-I | sed "s|\-I||g"`
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --build=$CHOST \
      --enable-shared \
      --with-threads \
      --with-computed-gotos \
      --with-system-expat \
      --with-dbmliborder=gdbm:ndbm \
      --with-system-ffi    
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/Python-${pkgver}/build-${_arch}"
    make install DESTDIR="$pkgdir"
    install -m 644 libpython${_pybasever}.dll.a "$pkgdir"/usr/${_arch}/lib
    rm -rf "$pkgdir"/usr/${_arch}/share
    rm "$pkgdir"/usr/${_arch}/bin/2to3*
    rm "$pkgdir"/usr/${_arch}/bin/idle*
    rm "$pkgdir"/usr/${_arch}/bin/pydoc*
    rm "$pkgdir"/usr/${_arch}/bin/pyvenv*
    rm "$pkgdir"/usr/${_arch}/bin/python-config.sh
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
