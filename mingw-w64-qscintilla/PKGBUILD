# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-qscintilla
pkgver=2.7.2
pkgrel=3
pkgdesc="A port to Qt4 of Neil Hodgson's Scintilla C++ editor class (mingw-w64)"
license=('GPL')
arch=(any)
url="http://www.riverbankcomputing.co.uk/software/qscintilla/intro"
depends=('mingw-w64-crt' 'mingw-w64-qt4')
makedepends=('mingw-w64-gcc' 'mingw-w64-qt4')
options=('!buildflags' '!strip' 'staticlibs')
source=("http://downloads.sourceforge.net/pyqt/QScintilla-gpl-${pkgver}.tar.gz")
md5sums=('0fbfb06078f206a241a62c94861cce21')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"      

build() {
  unset CFLAGS CXXFLAGS LDFLAGS
  cd "$srcdir/QScintilla-gpl-${pkgver}"
  for _arch in ${_architectures}; do
    export QTDIR=/usr/${_arch}
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    ${QTDIR}/bin/qmake ../Qt4Qt5/qscintilla.pro
    sed -i "s|libqscintilla2.a|libqscintilla2.dll.a|g" Makefile.Release
    make
    popd
  done
}


package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/QScintilla-gpl-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" INSTALL_ROOT="$pkgdir" install
    install -d "$pkgdir"/usr/${_arch}/bin
    mv "$pkgdir"/usr/${_arch}/lib/qscintilla2.dll "$pkgdir"/usr/${_arch}/bin
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm -rf "$pkgdir"/usr/${_arch}/share
  done
}
