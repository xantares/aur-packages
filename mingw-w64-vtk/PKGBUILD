
pkgname=mingw-w64-vtk
pkgver=5.8.0
pkgrel=1
pkgdesc='A software system for 3D computer graphics, image processing, and visualization. (mingw-w64)'
arch=('any')
url='http://www.vtk.org/'
license=('BSD')
depends=('mingw-w64-crt' 'mingw-w64-libjpeg-turbo' 'mingw-w64-qt4' 'mingw-w64-expat' 'mingw-w64-libtiff' 'mingw-w64-libjpeg-turbo' 'mingw-w64-freetype' 'mingw-w64-libpng' 'mingw-w64-libxml2' 'mingw-w64-hdf5' 'mingw-w64-libodbc++')
makedepends=('mingw-w64-cmake')
options=('!buildflags' 'staticlibs' '!strip')
source=("http://www.vtk.org/files/release/5.8/vtk-${pkgver}.tar.gz"
        vtk-1-tryrun.patch
        vtk-2-findfreetype.patch
        vtk-3-compile-tools.patch
        vtk-4-native-no-gui.patch
        vtk-5-module-path.patch
        vtk-6-kfreebsd.patch
        vtk-7-mingw-w64-git.patch
        vtk-8-mingw-w64-extra.patch
        )
md5sums=('37b7297d02d647cc6ca95b38174cb41f'
         '01728872ef458dd1088020cbac5c211b'
         '93addb17de7f5f3474b30a9a73466df3'
         '65c5d98588d786922c6524f518e071d9'
         'f4d2ca63ea95f3416b642bd65c90193b'
         '9bae7925ae2d3c75d52cdf730bf5d181'
         'a994d1054e48604e6af1e2886d6fafa8'
         '847cb73c8da46b7cea655c1e30e9a4a9'
         'cdfa84161818ec34a0fd61c7d8100935')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}"/VTK

  # mxe patches
  patch -Np1 -i "${srcdir}"/vtk-1-tryrun.patch
  patch -Np1 -i "${srcdir}"/vtk-2-findfreetype.patch
  patch -Np1 -i "${srcdir}"/vtk-3-compile-tools.patch
  patch -Np1 -i "${srcdir}"/vtk-4-native-no-gui.patch
  patch -Np1 -i "${srcdir}"/vtk-5-module-path.patch
  patch -Np1 -i "${srcdir}"/vtk-6-kfreebsd.patch
  patch -Np1 -i "${srcdir}"/vtk-7-mingw-w64-git.patch
  patch -Np1 -i "${srcdir}"/vtk-8-mingw-w64-extra.patch

}

build() {
  cd "${srcdir}/VTK"

  # native build
  mkdir -p build-native && pushd build-native
  cmake -DBUILD_TESTING=OFF -DVTK_USE_RENDERING=OFF ..
  make CompileTools
  popd

  # flags to enable using system libs
  local cmake_system_flags=""
  for lib in HDF5 EXPAT JPEG PNG TIFF ZLIB LIBXML2 OGGTHEORA; do
    cmake_system_flags+="-DVTK_USE_SYSTEM_${lib}:BOOL=ON "
  done

  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -C $PWD/../TryRunResults.cmake \
      -DBUILD_TESTING=OFF \
      -DVTKCompileTools_DIR=$PWD/../build-native \
      ${cmake_system_flags} \
      -DVTK_USE_QT=ON \
      -DVTK_USE_POSTGRES=ON \
      -DVTK_USE_ODBC=OFF \
      -DCMAKE_USE_PTHREADS=ON \
      -DCMAKE_USE_WIN32_THREADS=OFF \
      ..
    make
    popd
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -C $PWD/../TryRunResults.cmake \
      -DBUILD_TESTING=OFF \
      -DVTKCompileTools_DIR=$PWD/../build-native \
      ${cmake_system_flags} \
      -DVTK_USE_QT=ON \
      -DVTK_USE_POSTGRES=ON \
      -DVTK_USE_ODBC=OFF \
      -DCMAKE_USE_PTHREADS=ON \
      -DCMAKE_USE_WIN32_THREADS=OFF \
      -DBUILD_SHARED_LIBS=OFF \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/VTK/build-${_arch}-static"
    make install DESTDIR="$pkgdir"
    cd "$srcdir/VTK/build-${_arch}"
    make install DESTDIR="$pkgdir"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/vtk-5.8/*.a
  done
}

