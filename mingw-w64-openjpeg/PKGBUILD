# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-openjpeg
pkgver=1.5.1
pkgrel=4
pkgdesc="An open source JPEG 2000 codec (mingw-w64)"
arch=(any)
url="http://www.openjpeg.org"
license=("BSD")
makedepends=('mingw-w64-gcc' 'mingw-w64-lcms2' 'mingw-w64-libtiff' 'mingw-w64-libpng' 'mingw-w64-zlib')
depends=('mingw-w64-crt' 'mingw-w64-lcms2' 'mingw-w64-libtiff' 'mingw-w64-libpng' 'mingw-w64-zlib')
options=('!libtool' '!strip' '!buildflags')
source=("http://openjpeg.googlecode.com/files/openjpeg-${pkgver}.tar.gz"
        "cdecl.patch"
        "openjpeg-1.5_tiff-pkgconfig.patch"
        "openjpeg-1.5-r2029.patch"
        "openjpeg-1.5-r2031.patch"
        "openjpeg-1.5-r2032.patch"
        "openjpeg-1.5-r2033.patch")
        
md5sums=('b5f74cec2688fb918331bb014061be6f'
         '673edf457f9b4f3fdc9a083e1857a76a'
         '7d4244a290c6f8d6777a7bd68706a7ef'
         '3f7235e2e18f3a9953778d6e9d25cb81'
         '4154f1f9fcc3c8a706d3741cda24295f'
         'e2dabc34959eacc5f8122aa521ece4e1'
         'ff9e5cdb269571563e075ccf88e1e1eb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/openjpeg-$pkgver"
  rm -r thirdparty
  patch -Np1 -i $srcdir/cdecl.patch
  patch -Np1 -i $srcdir/openjpeg-1.5_tiff-pkgconfig.patch
  patch -Np0 -i $srcdir/openjpeg-1.5-r2029.patch
  patch -Np0 -i $srcdir/openjpeg-1.5-r2031.patch
  patch -Np0 -i $srcdir/openjpeg-1.5-r2032.patch
  patch -Np0 -i $srcdir/openjpeg-1.5-r2033.patch
}

build() {
  cd "$srcdir/openjpeg-$pkgver"
  autoreconf -i
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "build-${_arch}" && pushd "build-${_arch}"
    export PKG_CONFIG_LIBDIR=/usr/${_arch}/lib/pkgconfig/
    ../configure \
      --prefix=/usr/${_arch} \
      --host=${_arch} \
      --disable-doc \
      --enable-jpwl
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/openjpeg-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm -r "$pkgdir/usr/${_arch}/share"
    ${_arch}-strip -g -x $pkgdir/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g -x $pkgdir/usr/${_arch}/bin/*.exe
    ${_arch}-strip -g $pkgdir/usr/${_arch}/lib/*.a
  done
}
