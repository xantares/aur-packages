
pkgname=mingw-w64-polyclipping
pkgver=6.1.3a
pkgrel=1
pkgdesc="Polygon clipping library (mingw-w64)"
arch=('any')
url="http://sourceforge.net/projects/polyclipping/"
license=('custom')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-cmake' 'dos2unix')
options=('!buildflags' 'staticlibs' '!strip')
source=("http://downloads.sourceforge.net/polyclipping/clipper_ver${pkgver}.zip"
        'polyclipping.patch')
md5sums=('4dcd043ce48de59714f07bd3ec7ac62b' 'SKIP')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  # Correct line ends and encodings
  find . -type f -exec dos2unix -k {} \;

  # fedora patch
  patch -p0 -i ../polyclipping.patch

  # install static libs
  sed -i "s|LIBRARY DESTINATION|ARCHIVE DESTINATION lib LIBRARY DESTINATION|g" cpp/CMakeLists.txt
}

build() {
  cd "$srcdir"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF \
      ../cpp
    make
    popd
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      ../cpp
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/build-${_arch}-static"
    make install DESTDIR="$pkgdir"
    cd "$srcdir/build-${_arch}"
    make install DESTDIR="$pkgdir"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
