pkgname=mingw-w64-libltdl
pkgver=2.4.2
pkgrel=2
pkgdesc="A system independent dlopen wrapper for GNU libtool (mingw-w64)"
arch=(any)
url="http://www.gnu.org/software/libtool"
license=("GPL")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(!libtool !strip !buildflags)
source=("ftp://ftp.gnu.org/pub/gnu/libtool/libtool-${pkgver}.tar.xz")
md5sums=('2ec8997e0c07249eb4cbd072417d70fe')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    ${srcdir}/libtool-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.exe' | xargs -rtl1 rm
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
    rm -r "$pkgdir/usr/${_arch}/share"
    rm -r "$pkgdir/usr/${_arch}/bin/libtool"{,ize}
  done
}