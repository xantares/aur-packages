# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libsodium
pkgver=0.4.5
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl, the Networking and Cryptography library (mingw-w64)"
arch=('any')
url="https://github.com/jedisct1/libsodium"
license=("Public Domain")
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!strip' '!buildflags')
source=("http://download.dnscrypt.org/libsodium/releases/libsodium-${pkgver}.tar.gz")
md5sums=('3a97d2c29ec0133e334036e3f19d0741')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir"/libsodium-${pkgver}
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/libsodium-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/lib/*.la
    rm "$pkgdir"/usr/${_arch}/bin/*.def
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

