# Maintainer: xantares <xantares09 at hotmail dot com>

_pkgname=libsodium
pkgname=mingw-w64-$_pkgname
pkgver=0.4.3
pkgrel=1
pkgdesc="P(ortable|ackageable) NaCl, the Networking and Cryptography library (mingw-w64)"
arch=('any')
url="https://github.com/jedisct1/libsodium"
license=("Public Domain")
makedepends=('mingw-w64-gcc' 'autoconf')
depends=('mingw-w64-crt')
options=('!libtool' '!strip' '!buildflags')
source=("http://download.dnscrypt.org/${_pkgname}/releases/${_pkgname}-${pkgver}.tar.gz"
        'libsodium-def.patch')
md5sums=('7075953917e2b4d76423a51aab309b2a'
         '1d4114772de0c2e90cec769f0671b6ac')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  patch -Np1 -i "$srcdir"/libsodium-def.patch
  autoreconf -vfi
  rm src/libsodium/include/sodium/crypto_stream_salsa20.h
  rm src/libsodium/include/sodium/crypto_scalarmult_curve25519.h
}

build() {
  cd "$srcdir"/${_pkgname}-${pkgver}
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${_pkgname}-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    rm "$pkgdir"/usr/${_arch}/lib/*.la
    rm "$pkgdir"/usr/${_arch}/bin/*.def
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

