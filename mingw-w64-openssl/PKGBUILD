# Contributor: ant32 <antreimer@gmail.com>
# Contributor: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-openssl
_ver=1.0.1j
pkgver=1.0.1.j
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
options=('!strip' '!buildflags' '!makeflags' 'staticlibs')
license=('BSD')
url="http://www.openssl.org"
source=("http://www.openssl.org/source/openssl-$_ver.tar.gz"
        'no-rpath.patch')
sha256sums=('1b60ca8789ba6f03e8ef20da2293b8dc131c39d83814e775069f02d26354edf3'
            '754d6107a306311e15a1db6a1cc031b81691c8b9865e8809ac60ca6f184c957c')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/openssl-$_ver"

  # remove rpath: http://bugs.archlinux.org/task/14367
  patch -p0 -i "${srcdir}/no-rpath.patch"
}

build() {
  cd "${srcdir}/openssl-$_ver"

  # No support for out-of-source builds => create two copies
  for _arch in ${_architectures}; do
    mkdir -p "${srcdir}/build-${_arch}"
    cp -a "${srcdir}/openssl-$_ver/"* "${srcdir}/build-${_arch}"

    # Use mingw cflags instead of hardcoded ones
    sed -i -e '/^"mingw"/ s/-fomit-frame-pointer -O3 -march=i486 -Wall/-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4/' ${srcdir}/build-${_arch}/Configure
  done

  for _arch in ${_architectures}; do
    _mingw=mingw
    [ "${_arch}" = 'x86_64-w64-mingw32' ] && _mingw=mingw64
    cd "${srcdir}/build-${_arch}"
    unset LDFLAGS
    ./Configure \
      --prefix=/usr/${_arch} \
      --cross-compile-prefix=${_arch}- \
      -DOPENSSL_NO_CAPIENG \
      ${_mingw} \
      shared \
      zlib-dynamic
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/build-${_arch}"
    make -j1 INSTALL_PREFIX="${pkgdir}" install
    install -m644 ms/applink.c "${pkgdir}/usr/${_arch}/include/openssl/"
    ${_arch}-strip --strip-all "${pkgdir}/usr/${_arch}/bin/"*.exe
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/bin/"*.dll
    ${_arch}-strip --strip-debug "${pkgdir}/usr/${_arch}/lib/"*.a
    ${_arch}-strip --strip-unneeded "${pkgdir}/usr/${_arch}/lib/engines/"*.dll
    rm -r "${pkgdir}/usr/${_arch}/"{bin/c_rehash,ssl/misc,ssl/man}
  done
}
