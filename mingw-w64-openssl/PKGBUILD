# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-openssl
pkgver=1.0.1e
pkgrel=4
arch=('any')
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (mingw-w64)"
depends=('mingw-w64-crt' 'mingw-w64-zlib')
makedepends=('mingw-w64-gcc')
options=(!strip !buildflags staticlibs)
license=('BSD')
url="http://www.openssl.org"
source=("http://openssl.org/source/openssl-$pkgver.tar.gz"
        "no-rpath.patch"
        'openssl-1.0.1e-backports.patch'
        'openssl-1.0.1e-manfix.patch')
md5sums=('66bf6f10f060d561929de96f9dfe5b8c'
         'dc78d3d06baffc16217519242ce92478'
         '9ae1f46fee663c7e8ffb205e129d0539'
         'f14e7a5143fd3deac4ff0d2727b16fd7')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build()
{
  cd "${srcdir}/openssl-${pkgver}"

  # remove rpath: http://bugs.archlinux.org/task/14367
  patch -p0 -i ${srcdir}/no-rpath.patch

  # fix man generation with newer perl
  patch -p1 -i ${srcdir}/openssl-1.0.1e-backports.patch
  patch -p1 -i ${srcdir}/openssl-1.0.1e-manfix.patch

  # No support for out-of-source builds => create two copies
  for _arch in ${_architectures}; do
    mkdir -p $srcdir/build-${_arch}
    cp -a ${srcdir}/openssl-${pkgver}/* ${srcdir}/build-${_arch}

    # Use mingw cflags instead of hardcoded ones
    sed -i -e '/^"mingw"/ s/-fomit-frame-pointer -O3 -march=i486 -Wall/-O2 -g -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4/' ${srcdir}/build-${_arch}/Configure
  done

  for _arch in ${_architectures}; do
    _mingw=mingw
    if $(echo ${_arch} | grep -q x86_64); then
      _mingw=mingw64
    fi
    cd "$srcdir/build-${_arch}"
    unset LDFLAGS
    ./Configure \
      --prefix=/usr/${_arch} \
      --cross-compile-prefix=${_arch}- \
      -DOPENSSL_NO_CAPIENG \
      ${_mingw} \
      shared \
      zlib-dynamic
    make -j1
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/build-${_arch}"
    make -j1 INSTALL_PREFIX="$pkgdir" install
    install -m644 ms/applink.c "${pkgdir}"/usr/${_arch}/include/openssl/
    ${_arch}-strip --strip-all "$pkgdir"/usr/${_arch}/bin/*.exe
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip --strip-debug "$pkgdir"/usr/${_arch}/lib/*.a
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/lib/engines/*.dll
    rm -rf "$pkgdir"/usr/${_arch}/{bin/c_rehash,ssl/misc,ssl/man}
  done
}
