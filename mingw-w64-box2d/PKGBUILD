pkgname=mingw-w64-box2d
pkgver=2.3.0
pkgrel=1
pkgdesc="2D rigid body simulation library for games (mingw-w64)"
arch=(any)
url="http://www.box2d.org/"
license=("zlib")
makedepends=(mingw-w64-cmake unzip)
depends=(mingw-w64-crt)
options=(staticlibs !strip !buildflags)
source=("http://box2d.googlecode.com/files/Box2D_v${pkgver}.7z")
md5sums=('696a457fc6260a9bb6d2c380b9100279')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/Box2D_v$pkgver/Box2D"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DBOX2D_INSTALL:BOOL=ON \
      -DBOX2D_BUILD_EXAMPLES:BOOL=OFF \
      -DBOX2D_BUILD_SHARED:BOOL=ON \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/Box2D_v$pkgver/Box2D/build-${_arch}"
    make DESTDIR="$pkgdir" install
    mkdir -p "$pkgdir/usr/$_arch/bin"
    mv "$pkgdir/usr/${_arch}/lib/"*.dll "$pkgdir/usr/${_arch}/bin"
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
