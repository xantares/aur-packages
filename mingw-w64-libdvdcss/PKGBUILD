
pkgname=mingw-w64-libdvdcss
pkgver=1.2.13
pkgrel=1
pkgdesc="Portable abstraction library for DVD decryption (mingw-w64)"
url="http://www.videolan.org/libdvdcss"
arch=('any')
license=('GPL')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('staticlibs' '!buildflags' '!strip')
source=(http://download.videolan.org/pub/libdvdcss/$pkgver/libdvdcss-$pkgver.tar.bz2)
sha1sums=('1a4a5e55c7529da46386c1c333340eee2c325a77')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd libdvdcss-$pkgver
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir"/libdvdcss-${pkgver}/build-${_arch}
    make install DESTDIR="$pkgdir"
    rm -r "$pkgdir"/usr/${_arch}/share
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
