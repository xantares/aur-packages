
pkgname=psp-newlib
pkgver=1.20.0
pkgrel=1
pkgdesc="Newlib is a C library intended for use on embedded systems (psp)"
arch=(any)
url="http://sourceware.org/newlib/"
license=('GPL')
groups=('psp')
makedepends=('psp-gcc-base' 'psp-binutils' 'psp-sdk-base')
options=('!buildflags' '!strip' 'staticlibs')
source=("ftp://sourceware.org/pub/newlib/newlib-$pkgver.tar.gz"
        "newlib-$pkgver-PSP.patch")
md5sums=('e5488f545c46287d360e68a801d470e8'
         '299d12b29bd492fe21bb27f72f1de548')

prepare ()
{
  cd "$srcdir/newlib-$pkgver"
  
  rm -rf newlib/libc/sys/psp/
  patch -p1 -i "$srcdir"/newlib-$pkgver-PSP.patch
}

build()
{
  cd "$srcdir/newlib-$pkgver"
  mkdir -p build-psp && pushd build-psp
  ../configure --prefix=/usr --target=psp --enable-newlib-iconv --enable-newlib-multithread --enable-newlib-mb 
  make 
}

package()
{
  cd "$srcdir/newlib-$pkgver/build-psp"
  make install DESTDIR="$pkgdir"
  rm -r "$pkgdir"/usr/share
}

