pkgname=openturns
pkgver=1.4
pkgrel=2
pkgdesc="Uncertainty treatment library in C++/Python"
license=('LGPL')
arch=('i686' 'x86_64')
url="http://www.openturns.org/"
depends=('lapack' 'libxml2' 'muparser' 'intel-tbb')
optdepends=('r: plotting, linear model, truncated normal distribution estimation and statistical tests'
            'python: python bindings'
            'python-matplotlib: nicer plotting')
makedepends=('python' 'r' 'cmake' 'gcc' 'gcc-fortran' 'bison' 'flex' 'swig' 'boost')
backup=('etc/openturns/openturns.conf')
source=("http://downloads.sourceforge.net/sourceforge/openturns/openturns/openturns-$pkgver.tar.bz2"
       "openturns-composeddistribution-elliptical-distribution-transformation.patch")
md5sums=('95ddef47c99ea2b009c61d16fa984792'
         'a6b96bde9e279e5b26a474dca9c4cb7d')

prepare() {
  cd $pkgname-$pkgver
  patch -p0 -i "$srcdir"/openturns-composeddistribution-elliptical-distribution-transformation.patch
  2to3 --no-diffs -w python/test/t_*.py
}

build() {
  cd $pkgname-$pkgver
  R CMD INSTALL --library=$PWD utils/rot_1.4.5.tar.gz
  export R_LIBS=$PWD
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DOPENTURNS_SYSCONFIG_PATH=/etc \
        -DSYSTEM_INSTALL=ON .
  make
}

package() {
  cd $pkgname-$pkgver
  mkdir -p "$pkgdir"/usr/lib/R/library/
  cp -r rot "$pkgdir"/usr/lib/R/library/
  make DESTDIR="$pkgdir" install
}

