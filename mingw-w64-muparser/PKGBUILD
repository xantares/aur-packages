# Maintainer: Ronald van Haren <ronald.archlinux.org>

_pkgname=muparser
pkgname=mingw-w64-$_pkgname
pkgver=2.2.3
pkgrel=1
pkgdesc="A fast math parser library (mingw-w64)"
arch=('i686' 'x86_64')
url="http://muparser.sourceforge.net/"
depends=('mingw-w64-crt')
license=('custom')
options=('!buildflags' '!libtool' '!strip')
source=("http://downloads.sourceforge.net/$_pkgname/${_pkgname}_v${pkgver//./_}.zip")
md5sums=('9de40ec1dab5bd2787ee344fce5846ad')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir"/${_pkgname}_v${pkgver//./_}
  for _arch in ${_architectures}; do
    unset LDFLAGS CFLAGS CPPFLAGS CXXFLAGS
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --disable-samples
    make
    popd
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ../configure --prefix=/usr/${_arch} \
                 --host=${_arch} \
                 --disable-shared \
                 --disable-samples
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do  
    cd "$srcdir"/${_pkgname}_v${pkgver//./_}/build-${_arch}
    make DESTDIR="$pkgdir" install
    install -d "$pkgdir"/usr/${_arch}/bin
    mv "$pkgdir"/usr/${_arch}/lib/*.dll "$pkgdir"/usr/${_arch}/bin
    cd "$srcdir"/${_pkgname}_v${pkgver//./_}/build-${_arch}-static
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

