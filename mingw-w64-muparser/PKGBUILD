# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-muparser
pkgver=2.2.3
pkgrel=9
pkgdesc="A fast math parser library (mingw-w64)"
arch=(any)
url="http://muparser.sourceforge.net/"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-cmake')
license=('MIT')
options=('!buildflags' 'staticlibs' '!strip')
source=("http://downloads.sourceforge.net/muparser/muparser_v${pkgver//./_}.zip"
        'CMakeLists.txt')
md5sums=('9de40ec1dab5bd2787ee344fce5846ad'
         'SKIP')
_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir"/muparser_v${pkgver//./_}
  #sed -i "s|//#define MUP_USE_OPENMP|#define MUP_USE_OPENMP|g" include/muParserDef.h
}


build() {
  cd "$srcdir"/muparser_v${pkgver//./_}
  cp "$srcdir"/CMakeLists.txt .
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      -DBUILD_SHARED_LIBS=OFF \
      ..
    make
    popd
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      -DCMAKE_BUILD_TYPE=Release \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do  
    cd "$srcdir/muparser_v${pkgver//./_}/build-${_arch}-static"
    make DESTDIR="$pkgdir" install
    cd "$srcdir/muparser_v${pkgver//./_}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

