pkgname=mingw-w64-mixmod
pkgver=3.0.0
pkgrel=1
pkgdesc="Model-Based supervised and unsupervised classification on qualitative and quantitative data."
arch=('any')
url="http://www.mixmod.org"
license=(GPL)
depends=('mingw-w64-crt')
makedepends=('mingw-w64-cmake')
options=(!buildflags !strip staticlibs)
source=("http://www.mixmod.org/IMG/tgz/mixmod_3-0-0_src.tgz"
        'mixmod3.patch')
md5sums=('0299f21422480a6f0f0ad7dd5e0096b9'
         '480c9e761c74f0218fc439ba924227f1')
_architecture="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir/mixmodLib_3_0_0"
  rm -f UseMixmod.cmake MixmodConfig.cmake.in
  patch -p1 -i "$srcdir"/mixmod3.patch
}

build () {
  cd "$srcdir/mixmodLib_3_0_0"
  for _arch in $_architecture; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake .. 
    make
    popd
  done
}

package () {
  cd "$srcdir/mixmodLib_3_0_0"
  for _arch in $_architecture; do
    cd "$srcdir/mixmodLib_3_0_0/build-${_arch}"
    make install DESTDIR="$pkgdir"
    rm -r "$pkgdir"/usr/${_arch}/share 
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}

