pkgname=mingw-w64-sdl2_mixer
pkgver=2.0.0
pkgrel=8
pkgdesc="SDL2 mixer libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_mixer"
license=("MIT")
depends=(mingw-w64-sdl2 mingw-w64-smpeg2 mingw-w64-libvorbis mingw-w64-libmodplug mingw-w64-flac)
makedepends=(mingw-w64-configure)
options=(staticlibs !strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-$pkgver.tar.gz")
md5sums=('65f6d80df073a1fb3bb537fbda031b50')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/SDL2_mixer-${pkgver}"

  # http://forums.libsdl.org/viewtopic.php?t=10170&sid=17f6449f7d0176ab18eec72b1c7c9363
  sed -i "s|#include \"modplug.h\"|#include \"libmodplug/modplug.h\"|g" music_modplug.h dynamic_modplug.h 
}

build() {
  cd "${srcdir}/SDL2_mixer-${pkgver}"
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-configure \
      --disable-sdltest \
      --disable-smpegtest \
      --with-smpeg-prefix=/usr/${_arch}
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/SDL2_mixer-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
