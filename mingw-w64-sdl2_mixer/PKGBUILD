pkgname=mingw-w64-sdl2_mixer
pkgver=2.0.0
pkgrel=7
pkgdesc="SDL2 mixer libraries (mingw-w64)"
arch=(any)
url="http://www.libsdl.org/projects/SDL_mixer"
license=("MIT")
makedepends=(mingw-w64-gcc
mingw-w64-pkg-config
mingw-w64-smpeg2
mingw-w64-libvorbis
mingw-w64-libmodplug
mingw-w64-flac)
depends=(mingw-w64-crt mingw-w64-sdl2)
optdepends=("mingw-w64-flac: FLAC support"
"mingw-w64-libmodplug: MOD support"
"mingw-w64-libvorbis: Ogg-Vorbis support"
"mingw-w64-smpeg2: MP3 support")
options=(staticlibs !strip !buildflags)
source=("http://www.libsdl.org/projects/SDL_mixer/release/SDL2_mixer-$pkgver.tar.gz")
md5sums=('65f6d80df073a1fb3bb537fbda031b50')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "${srcdir}/SDL2_mixer-${pkgver}"

  # http://forums.libsdl.org/viewtopic.php?t=10170&sid=17f6449f7d0176ab18eec72b1c7c9363
  sed -i "s|#include \"modplug.h\"|#include \"libmodplug/modplug.h\"|g" music_modplug.h dynamic_modplug.h 
}

build() {
  for _arch in ${_architectures}; do
    unset LDFLAGS
    mkdir -p "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    "${srcdir}"/SDL2_mixer-${pkgver}/configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --disable-sdltest \
      --disable-smpegtest \
      --with-smpeg-prefix=/usr/${_arch}
    make
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/${pkgname}-${pkgver}-build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip --strip-unneeded
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}
