pkgbase=mingw-w64-gmp
pkgname=(mingw-w64-gmp mingw-w64-gmp-static)
pkgver=6.0.0
pkgrel=1
pkgdesc="A free library for arbitrary precision arithmetic (mingw-w64)"
arch=(any)
url="http://gmplib.org"
license=("LGPL3")
makedepends=(mingw-w64-gcc)
depends=(mingw-w64-crt)
options=(staticlibs !strip !buildflags)
source=("ftp://ftp.gmplib.org/pub/gmp-${pkgver}/gmp-${pkgver}.tar.xz")
md5sums=('4dc314ac6d3059cc8d712aad4a6d162e')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/gmp-${pkgver}"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch}-static && pushd build-${_arch}-static
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-cxx \
      --disable-shared \
      --enable-static
    make
    popd
    mkdir -p build-${_arch} && pushd build-${_arch}
    ../configure \
      --prefix=/usr/${_arch} \
      --build=$CHOST \
      --host=${_arch} \
      --enable-cxx \
      --enable-shared \
      --disable-static
    make
    popd
  done
}

package_mingw-w64-gmp() {
  for _arch in ${_architectures}; do
    cd "${srcdir}/gmp-${pkgver}/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a 
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}


package_mingw-w64-gmp-static() {
  pkgdesc="A free library for arbitrary precision arithmetic (mingw-w64)"
  conflicts=(mingw-w64-gmp)
  provides=(mingw-w64-gmp)
  for _arch in ${_architectures}; do
    cd "${srcdir}/gmp-${pkgver}/build-${_arch}-static"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
    rm -r "$pkgdir/usr/${_arch}/share"
  done
}

