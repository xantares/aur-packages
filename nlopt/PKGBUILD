# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=nlopt
pkgver=2.4.1
pkgrel=2
pkgdesc="a non-linear optimization package, callable from C, C++, Fortran, Matlab \
or GNU Octave, Python, GNU Guile, and GNU R."
arch=('i686' 'x86_64')
url="http://ab-initio.mit.edu/wiki/index.php/NLopt"
license=('LGPL')
depends=('gcc-libs')
makedepends=('python-numpy' 'octave' 'guile' 'swig')
optdepends=('octave: to use with octave',
            'python: to use with python',
            'guile: to use with guile')
options=()
source=("http://ab-initio.mit.edu/nlopt/$pkgname-$pkgver.tar.gz")
md5sums=('ed72dd533b641d7e1f93eb790dee828c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  export LDFLAGS="${LDFLAGS//,--as-needed}"
  mkdir -p build_cxx
  cd build_cxx
  ../configure --prefix=/usr --enable-shared --with-cxx --enable-maintainer-mode
  make
  cd ..
  mkdir -p build
  cd build
  ../configure --prefix=/usr --enable-shared --without-python --without-guile 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build_cxx"
  make DESTDIR="$pkgdir" install
  cd ../build
  make DESTDIR="$pkgdir" install
}

