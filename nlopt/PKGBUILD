# Maintainer: David Kremer <david.kremer.dk@gmail.com>

pkgname=nlopt
pkgver=2.3
pkgrel=5
pkgdesc="a non-linear optimization package, callable from C, C++, Fortran, Matlab \
or GNU Octave, Python, GNU Guile, and GNU R."
arch=('i686' 'x86_64')
url="http://ab-initio.mit.edu/wiki/index.php/NLopt"
license=('LGPL')
depends=('gcc-libs')
makedepends=('libtool' 'automake' 'autoconf' 'gcc' 'python2-numpy' 'octave' 'guile' 'swig')
optdepends=('octave: for use with octave',
            'python2: for use with python',
            'guile: for use with the guile proramming language')
options=('!libtool')
source=("http://ab-initio.mit.edu/nlopt/$pkgname-$pkgver.tar.gz")
md5sums=('811a9f1c7a7f879c7d7b4caa059eb8d6')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i "s|top_builddir)/api|top_srcdir)/api|g" swig/Makefile.am
  autoreconf --verbose --install --symlink --force
  mkdir -p build_cxx
  cd build_cxx
  ../configure --prefix=/usr --enable-shared PYTHON=python2 --without-guile --with-cxx
  make
  cd ..
  mkdir -p build
  cd build
  ../configure --prefix=/usr --enable-shared PYTHON=python2 --without-guile
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build_cxx"
  make DESTDIR="$pkgdir" install
  cd ../build
  make DESTDIR="$pkgdir" install
}
