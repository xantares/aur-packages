# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=nlopt
pkgver=2.4
pkgrel=1
pkgdesc="a non-linear optimization package, callable from C, C++, Fortran, Matlab \
or GNU Octave, Python, GNU Guile, and GNU R."
arch=('i686' 'x86_64')
url="http://ab-initio.mit.edu/wiki/index.php/NLopt"
license=('LGPL')
depends=('gcc-libs')
makedepends=('python-numpy' 'octave' 'guile' 'swig')
optdepends=('octave: to use with octave',
            'python: to use with python',
            'guile: to use with guile')
options=()
source=("http://ab-initio.mit.edu/nlopt/$pkgname-$pkgver.tar.gz")
md5sums=('05a50139340ba25cced8c48c48896286')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  export CXXFLAGS="$CXXFLAGS -fpermissive"
  mkdir -p build_cxx
  cd build_cxx
  ../configure --prefix=/usr --enable-shared --with-cxx --enable-maintainer-mode
  make
  cd ..
  mkdir -p build
  cd build
  ../configure --prefix=/usr --enable-shared --without-python --without-guile 
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver/build_cxx"
  make DESTDIR="$pkgdir" install
  cd ../build
  make DESTDIR="$pkgdir" install
}

