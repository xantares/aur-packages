pkgname=mingw-w64-taglib
pkgver=1.8
pkgrel=1
pkgdesc="A Library for reading and editing the meta-data of several popular audio formats (mingw-w64)"
arch=(any)
url="http://taglib.github.com"
license=("GPL")
makedepends=(mingw-w64-gcc cmake)
depends=(mingw-w64-crt mingw-w64-zlib)
conflicts=(mingw-w64-taglib-static)
provides=(mingw-w64-taglib-static)
options=(!libtool !strip !buildflags)
source=("https://github.com/downloads/taglib/taglib/taglib-$pkgver.tar.gz"
"Toolchain-i686-w64-mingw32.cmake"
"Toolchain-x86_64-w64-mingw32.cmake")
md5sums=('dcb8bd1b756f2843e18b1fdf3aaeee15'
         'da10ad00a161800e0b011a77c3efbf76'
         'adea9ccdeea1784c7cd30af195f2a8cb')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "$srcdir/${pkgname#mingw-w64-}-$pkgver"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir "build-${_arch}" && pushd "build-${_arch}"
    PATH="/usr/${_arch}/bin:$PATH" cmake \
      -DCMAKE_INSTALL_PREFIX:PATH=/usr/${_arch} \
      -DBUILD_SHARED_LIBS:BOOL=ON \
      -DWITH_ASF:BOOL=ON \
      -DWITH_MP4:BOOL=ON \
      -DCMAKE_TOOLCHAIN_FILE=${srcdir}/Toolchain-${_arch}.cmake \
      ..
    make
    popd
  done
}

package() {

  for _arch in ${_architectures}; do
    cd "$srcdir/${pkgname#mingw-w64-}-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    find "$pkgdir/usr/${_arch}" -name '*.dll' | xargs -rtl1 ${_arch}-strip -x
    find "$pkgdir/usr/${_arch}" -name '*.a' -o -name '*.dll' | xargs -rtl1 ${_arch}-strip -g
  done
}