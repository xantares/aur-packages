# Contributor: vldmr <vldmr@lavabit.com>
# Contributor: Thomas Krug <phragment@lavabit.com>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>

pkgname=libfprint-git
pkgver=261.35e356f
pkgrel=1
pkgdesc="Library for fingerprint readers"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/libfprint"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
groups=(fprint-git)
provides=(libfprint)
conflicts=(libfprint)
source=("git+git://anongit.freedesktop.org/libfprint/libfprint.git")
md5sums=('SKIP')

pkgver() {
  cd "${srcdir}/libfprint"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/libfprint"
  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --disable-static \
               --disable-examples-build \
               --disable-x11-examples-build
  make
}

package() {
  cd "${srcdir}/libfprint"
  make DESTDIR="$pkgdir/" install
  install -d "$pkgdir/lib/udev/rules.d"
  mv "$pkgdir"/usr/lib/udev/rules.d/* "$pkgdir/lib/udev/rules.d/"
  
  # generate udev rules from autosuspend rules
  cp "$pkgdir"/lib/udev/rules.d/60-fprint-autosuspend.rules "$pkgdir"/lib/udev/rules.d/50-fprint.rules
  sed -i "s|SUBSYSTEM==\"usb\", ||g" "$pkgdir"/lib/udev/rules.d/50-fprint.rules
  sed -i "s|ATTRS{dev}==\"\*\", ATTR{power/control}=\"auto\"|TAG+=\"uaccess\"|g" "$pkgdir"/lib/udev/rules.d/50-fprint.rules
}
