# Maintainer: vldmr <vldmr@lavabit.com>
# Maintainer: Thomas Krug <phragment@lavabit.com>
# Contributor: Matthew Bauer <mjbauer95@gmail.com>

pkgname=libfprint-git
_gitname="libfprint"
pkgver=261.35e356f
pkgrel=1
pkgdesc="Library for fingerprint readers"
arch=(i686 x86_64)
url="http://www.freedesktop.org/wiki/Software/fprint/libfprint"
license=(LGPL)
depends=(libusb nss gdk-pixbuf2)
makedepends=(git)
groups=(fprint-git)
provides=(libfprint)
conflicts=(libfprint)
options=(!libtool !emptydirs)
install=fprint.install
source=("git+git://anongit.freedesktop.org/libfprint/libfprint.git"
        '50-fprint.rules')
md5sums=('SKIP' 
         'a6ce2df005eba71dec2c6795f4ad84e8')

pkgver() {
  cd $_gitname
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${srcdir}/$_gitname"
  ./autogen.sh --prefix=/usr \
               --sysconfdir=/etc \
               --disable-static \
               --disable-examples-build \
               --disable-x11-examples-build
  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir/" install
  mkdir -p "$pkgdir/lib/udev/rules.d"
  mv "$pkgdir"/usr/lib/udev/rules.d/* "$pkgdir/lib/udev/rules.d/"
  install -m644 "$srcdir/50-fprint.rules" "$pkgdir/lib/udev/rules.d"
}
