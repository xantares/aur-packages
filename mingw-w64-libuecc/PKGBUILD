# Maintainer: xantares <xantares09 at hotmail dot com>

pkgname=mingw-w64-libuecc
pkgver=3
pkgrel=3
arch=(any)
pkgdesc="Very small Elliptic Curve Cryptography library (mingw-w64)"
depends=('mingw-w64-crt')
makedepends=('mingw-w64-cmake')
options=('!buildflags' '!strip' 'staticlibs')
license=("BSD")
url="http://git.universe-factory.net/libuecc"
source=("https://projects.universe-factory.net/attachments/download/42/libuecc-3.tar.xz"
       'libuecc-shared.patch')
md5sums=('3c45ffecc7709ea929892993808e218e'
         '5f738c6bad461d7f2f89b54cdd013ddc')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

prepare () {
  cd "$srcdir/libuecc-$pkgver"
  patch -p1 -i "$srcdir"/libuecc-shared.patch
}

build() {
  cd "$srcdir/libuecc-$pkgver"
  unset LDFLAGS
  for _arch in ${_architectures}; do
    mkdir -p build-${_arch} && pushd build-${_arch}
    ${_arch}-cmake \
      ..
    make
    popd
  done
}

package() {
  for _arch in ${_architectures}; do
    cd "$srcdir/libuecc-$pkgver/build-${_arch}"
    make DESTDIR="$pkgdir" install
    ${_arch}-strip --strip-unneeded "$pkgdir"/usr/${_arch}/bin/*.dll
    ${_arch}-strip -g "$pkgdir"/usr/${_arch}/lib/*.a
  done
}
