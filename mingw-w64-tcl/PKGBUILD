# Maintainer: Filip Brcic <brcha@gna.org>

pkgname=mingw-w64-tcl
_majorver1=8
_majorver2=5
_majorver="${_majorver1}.${_majorver2}"
pkgver="${_majorver}.11"
pkgrel=1
pkgdesc="MinGW Windows Tool Command Language, pronounced tickle (mingw-w64)"
arch=('any')
depends=('mingw-w64-crt')
makedepends=('mingw-w64-gcc')
options=('!libtool' '!strip')
license=('custom')
url="http://tcl.sourceforge.net/"
source=("http://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz"
        "tcl-8.5.1-autopath.patch"
	"tcl-8.5.10-conf.patch"
	"tcl-8.5.9-hidden.patch"
	"tcl-8.5.6-mingw32.patch"
	"tcl-nativetclsh.patch"
	"tcl-mingw-w64-compatibility.patch")
md5sums=('b01a9691c83990b3db0ce62d1012ca67'
         '1d649acd200ffdf27fcae441fceef833'
         '74a0e9236e5f9ed9e6a908bb90d34afd'
         'aead35e4382da6f755d798eb232092cc'
         '970896456778854b20dbfbc3855637cb'
         '39619a7718b27b860112241e70f3485c'
         '0be352d07a6f305618feee7bd79041f4')

_architectures="i686-w64-mingw32 x86_64-w64-mingw32"

build() {
  cd "${srcdir}/tcl${pkgver}"

  patch -Np1 -i ${srcdir}/tcl-8.5.1-autopath.patch
  patch -Np1 -i ${srcdir}/tcl-8.5.10-conf.patch
  patch -Np1 -i ${srcdir}/tcl-8.5.9-hidden.patch
  patch -Np0 -i ${srcdir}/tcl-8.5.6-mingw32.patch
  patch -Np0 -i ${srcdir}/tcl-nativetclsh.patch
  patch -Np0 -i ${srcdir}/tcl-mingw-w64-compatibility.patch

  unset LDFLAGS
  unset CFLAGS

  pushd win
  autoconf

  _arch="i686-w64-mingw32" # doesn't compile on 64bit arch, but keep pkgbuild with _arch for the future

  ./configure \
    --prefix=/usr/${_arch} \
    --host=${_arch} \
    --disable-threads \
    --enable-shared
  make -j1 TCL_LIBRARY=/usr/${_arch}/share/tcl${_majorver}

  popd # win
}

package() {
  cd "${srcdir}/tcl${pkgver}"

  _arch="i686-w64-mingw32" # doesn't compile on 64bit arch, but keep pkgbuild with _arch for the future

  make install -j1 -C win INSTALL_ROOT=${pkgdir} TCL_LIBRARY=/usr/${_arch}/share/tcl${_majorver}

  ln -s tclsh${_majorver1}${_majorver2}.exe ${pkgdir}/usr/${_arch}/bin/tclsh.exe

  mv ${pkgdir}/usr/${_arch}/lib/libtcl${_majorver1}${_majorver2}.a ${pkgdir}/usr/${_arch}/lib/libtcl${_majorver1}${_majorver2}.dll.a
  mv ${pkgdir}/usr/${_arch}/lib/libtclstub${_majorver1}${_majorver2}.a ${pkgdir}/usr/${_arch}/lib/libtclstub${_majorver1}${_majorver2}.dll.a
  
  # for linking with -libtcl
  ln -s libtcl${_majorver1}${_majorver2}.dll.a ${pkgdir}/usr/${_arch}/lib/libtcl.dll.a

  ln -s ../share/tcl${_majorver} ${pkgdir}/usr/${_arch}/lib/tcl${_majorver}

  # postgresql and maybe other packages too need tclConfig.sh
  # paths don't look at /usr/lib for efficiency, so we symlink into tcl8.5 for now
  ln -s /usr/${_arch}/lib/tclConfig.sh ${pkgdir}/usr/${_arch}/lib/tcl${_majorver}/tclConfig.sh

  mkdir -p ${pkgdir}/usr/${_arch}/include/tcl-private/{generic,win}
  find generic win -name "*.h" -exec cp -p '{}' ${pkgdir}/usr/${_arch}/include/tcl-private/'{}' ';'
  ( cd ${pkgdir}/usr/${_arch}/include
    for i in *.h ; do
      [ -f ${pkgdir}/usr/${_arch}/include/tcl-private/generic/$i ] && ln -sf ../../$i ${pkgdir}/usr/${_arch}/include/tcl-private/generic ;
    done
  ) || true

  # fix executable bits
  chmod a-x ${pkgdir}/usr/${_arch}/share/tcl${_majorver}/encoding/*.enc
  chmod a-x ${pkgdir}/usr/${_arch}/lib/*/pkgIndex.tcl

  # remove buildroot traces
  sed -i -e "s|$PWD/win|/usr/${_arch}/lib|; s|$PWD|/usr/${_arch}/include/tcl-private|" ${pkgdir}//usr/${_arch}/lib/tclConfig.sh
  rm -rf ${pkgdir}/usr/${_arch}/share/tcl${_majorver}/tclAppInit.c
  rm -rf ${pkgdir}/usr/${_arch}/share/tcl${_majorver}/ldAix

  # move windows packages to where tcl85.dll will find them
  mv ${pkgdir}//usr/${_arch}/lib/dde* ${pkgdir}//usr/${_arch}/lib/tcl${_majorver}/
  mv ${pkgdir}//usr/${_arch}/lib/reg* ${pkgdir}//usr/${_arch}/lib/tcl${_majorver}/

  # Strip
  ${_arch}-strip --strip-all ${pkgdir}/usr/${_arch}/bin/*.exe
  ${_arch}-strip --strip-unneeded ${pkgdir}/usr/${_arch}/bin/*.dll
  ${_arch}-strip --strip-debug ${pkgdir}/usr/${_arch}/lib/*.a
}

# vim:set ts=2 sw=2 et:
